def utils

def labelForOS = [
  'focal':     'linux',
  'jammy':      'linux',
  'rhel8':      'linux',
  'rhel9':      'linux',
  'opensuse15': 'linux',
  'windows':    'windows',
  'macos':      'macos'
]

pipeline {
  agent none

  options {
    disableConcurrentBuilds()
    buildDiscarder(
      logRotator(
        artifactDaysToKeepStr: '',
        artifactNumToKeepStr: '',
        daysToKeepStr: '',
        numToKeepStr: '100'))
  }
  
  parameters {
    booleanParam name: 'DAILY', defaultValue: true, description: 'Builds daily/nightly builds if true; builds hourly builds if false.'
    booleanParam name: 'PUBLISH', defaultValue: true, description: 'Publish the build to S3 and sentry.'
    booleanParam name: 'FORCE_BUILD_BINARIES', defaultValue: false, description: 'Force build binaries even if there are no changes, and even if they have already been built previously'
    booleanParam name: 'FORCE_BUILD_DOCKER', defaultValue: false, description: 'Force build docker images even if there are no dockerfile changes'
    string name: 'OS_FILTER', defaultValue: 'all', description: 'Pattern to limit builds by matching OS'
    string name: 'ARCH_FILTER', defaultValue: 'all', description: 'Pattern to limit builds by matching ARCH'
    string name: 'SLACK_CHANNEL', defaultValue: '#ide-builds', description: 'Slack channel to publish build message.', trim: true
  }
  
  environment {
    RSTUDIO_VERSION = ""
    RSTUDIO_VERSION_MAJOR = ""
    RSTUDIO_VERSION_MINOR = ""
    RSTUDIO_VERSION_PATCH = ""
    RSTUDIO_VERSION_SUFFIX = ""
    RSTUDIO_VERSION_FLOWER = ""
    COMMIT_HASH=""
    IS_PRO = false
    BUILD_BRANCH="${env.BRANCH_NAME.replace('/', '%2F')}"
  }

  stages {
    stage ("Set Version & Commit") {
      agent {
        dockerfile {
          filename 'Dockerfile.dispatcher'
          label 'linux'
        }
      }
      
      steps {
        script {
          utils = load "${env.WORKSPACE}/jenkins/utils.groovy"
          
          // Get the current commit
          COMMIT_HASH = sh returnStdout: true, script: 'git rev-parse HEAD'

          // Get the version
          (RSTUDIO_VERSION,
            RSTUDIO_VERSION_MAJOR,
            RSTUDIO_VERSION_MINOR,
            RSTUDIO_VERSION_PATCH,
            RSTUDIO_VERSION_SUFFIX) = utils.getVersion(!params.DAILY)

          RSTUDIO_VERSION_FLOWER = utils.getFlower()
          IS_PRO = RSTUDIO_VERSION_SUFFIX.contains('pro')
        }

        buildName "${RSTUDIO_VERSION}"
      }
    }
      
    stage('Rebuild Docker Images') {
      matrix {
        when {
          allOf {
            anyOf {
              expression { return params.FORCE_BUILD_DOCKER }
              changeset comparator: 'REGEXP', pattern: '((docker|dependencies)/.+|version/RELEASE)'
            }
            anyOf {
              equals expected: params.OS_FILTER, actual: env.os;
              equals expected: params.OS_FILTER, actual: 'all'
            }
            anyOf {
              equals expected: params.ARCH_FILTER, actual: env.arch;
              equals expected: params.ARCH_FILTER, actual: 'all'
            }
            not {
              allOf {
                equals expected: 'monitor', actual: env.imageType;
                expression { return !env.IS_PRO }  
              }
            }
          }
        }

        axes {
          axis {
            name 'os'
            values 'focal', 'jammy', 'rhel8', 'rhel9', 'opensuse15', 'windows'
          }
          axis {
            name 'arch'
            values 'x86_64', 'arm64'
          }
        }

        excludes {
          exclude {
            axis {
              name 'os'
              values 'rhel8', 'windows', 'opensuse15'
            }
            axis {
              name 'arch'
              values 'arm64'
            }
          }
        }

        stages {
          stage ("Pull Build Push") {
            agent {
              label "${labelForOS[os]} && ${arch}"
            }

            environment {
              GITHUB_LOGIN = credentials('posit-jenkins-rstudio')
              DOCKER_TAG = utils.getDockerTag()
              DOCKER_FILE = "docker/jenkins/Dockerfile.${env.os}"
            }

            steps {
              echo "Creating image jenkins/ide:${DOCKER_TAG}"
              pullBuildPush(
                image_name: 'jenkins/ide',
                image_tag: "${DOCKER_TAG}",
                latest_tag: false,
                build_arg_jenkins_uid: 'JENKINS_UID',
                build_arg_jenkins_gid: 'JENKINS_GID',
                dockerfile: "${DOCKER_FILE}",
                build_args: "--build-arg ARCH=${arch} --build-arg GITHUB_LOGIN=${GITHUB_LOGIN}",
                push: "${params.PUBLISH}"
              )
            }
          }
        }
      }
    }

    stage ("Setup and Trigger Builds") {
      when {
        anyOf {
          expression { return params.FORCE_BUILD_BINARIES }
          changeset comparator: 'REGEXP', pattern: '(?:(?!docs/).+|(?!jenkins/Jenkinsfile\\.build))'
        }
      }

      environment {
        JOB_ROOT = "IDE/${IS_PRO ? 'Pro' : 'OS'}-Builds/Platforms"
      }

      stages {
        stage ("Create a Sentry Release") {
          agent {
            dockerfile {
              filename 'Dockerfile.dispatcher'
              label 'linux'
            }
          }
          
          environment {
            SENTRY_API_KEY = credentials('ide-sentry-api-key')
          }

          when { expression { return params.PUBLISH && params.DAILY } }

          steps { 
            echo "Creating a sentry release for version ${RSTUDIO_VERSION}"

            // Install sentry
            sh "HOME=`pwd` ./dependencies/common/install-sentry-cli"

            // create new release on Sentry
            sh 'sentry-cli --auth-token ${SENTRY_API_KEY} releases --org rstudio --project ide-backend new ' + RSTUDIO_VERSION

            // associate commits
            sh 'sentry-cli --auth-token ${SENTRY_API_KEY} releases --org rstudio --project ide-backend set-commits --auto ' + RSTUDIO_VERSION

            // finalize release
            sh 'sentry-cli --auth-token ${SENTRY_API_KEY} releases --org rstudio --project ide-backend finalize ' + RSTUDIO_VERSION
          }
        }

        stage ("Build Windows") {
          when {
            anyOf {
              equals expected: labelForOS[params.OS_FILTER], actual: 'windows';
              equals expected: params.OS_FILTER, actual: 'all'
            }
          }

          steps {
            build wait: false,
                  job: "${JOB_ROOT}/windows-pipeline/${env.BUILD_BRANCH}",
                  parameters: [
                    gitParameter(name: "COMMIT_HASH", value: "${COMMIT_HASH}"),
                    booleanParam(name: "DAILY", value: params.DAILY),
                    booleanParam(name: "PUBLISH", value: params.PUBLISH),
                    booleanParam(name: "FORCE_BUILD_BINARIES", value: params.FORCE_BUILD_BINARIES),
                    string(name: "SLACK_CHANNEL", value: "${SLACK_CHANNEL}")
                  ]
          }
        }

        stage ("Build Linux") {
          when {
            anyOf {
              equals expected: labelForOS[params.OS_FILTER], actual: 'linux';
              equals expected: params.OS_FILTER, actual: 'all'
            }
          }

          steps {
            build wait: false,
                  job: "${JOB_ROOT}/linux-pipeline/${env.BUILD_BRANCH}",
                  parameters: [
                    gitParameter(name: "COMMIT_HASH", value: "${COMMIT_HASH}"),
                    booleanParam(name: "DAILY", value: params.DAILY),
                    booleanParam(name: "PUBLISH", value: params.PUBLISH),
                    booleanParam(name: "FORCE_BUILD_BINARIES", value: params.FORCE_BUILD_BINARIES),
                    string(name: "OS_FILTER", value: params.OS_FILTER),
                    string(name: "ARCH_FILTER", value: params.ARCH_FILTER),
                    string(name: "SLACK_CHANNEL", value: "${SLACK_CHANNEL}")
                  ]
          }
        }

        stage ("Build MacOS") {
          when {
            anyOf {
              equals expected: labelForOS[params.OS_FILTER], actual: 'macos';
              equals expected: params.OS_FILTER, actual: 'all'
            }
          }

          steps {
            build wait: false,
                  job: "${JOB_ROOT}/macos-pipeline/${env.BUILD_BRANCH}",
                  parameters: [
                    gitParameter(name: "COMMIT_HASH", value: "${COMMIT_HASH}"),
                    booleanParam(name: "DAILY", value: params.DAILY),
                    booleanParam(name: "PUBLISH", value: params.PUBLISH),
                    booleanParam(name: "FORCE_BUILD_BINARIES", value: params.FORCE_BUILD_BINARIES),
                    string(name: "SLACK_CHANNEL", value: "${SLACK_CHANNEL}")
                  ]
          }
        }
      }
    }
  }

  post {
    always {
      sendNotifications slack_channel: SLACK_CHANNEL
    }
  }
}
