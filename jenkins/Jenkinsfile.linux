def utils

def ext_map = 
[
  'focal':      'deb',
  'jammy':      'deb',
  'rhel8':      'rpm',
  'rhel9':      'rpm',
  'opensuse15': 'rpm'
]

def package_os =
[
  'focal':      'Ubuntu Focal',
  'jammy':      'Ubuntu Jammy',
  'rhel8':      'RHEL 8',
  'rhel9':      'RHEL 9',
  'opensuse15': 'OpenSUSE 15'
]

pipeline {
  agent none

  options {
    disableConcurrentBuilds()
    buildDiscarder(
      logRotator(
        artifactDaysToKeepStr: '',
        artifactNumToKeepStr: '',
        daysToKeepStr: '',
        numToKeepStr: '100'))
  }

  parameters {
    string(name: 'COMMIT_HASH' , defaultValue: '')
    booleanParam(name: 'DAILY', defaultValue: false, description: 'Runs daily build if true')
    booleanParam(name: 'PUBLISH', defaultValue: false, description: 'Runs publish stage if true')
    booleanParam(name: 'FORCE_BUILD_BINARIES', defaultValue: false, description: 'Force build binaries even if there are no changes, and even if they have already been built previously')
    string(name: 'OS_FILTER', defaultValue: 'all', description: 'Pattern to limit builds by matching OS')
    string(name: 'ARCH_FILTER', defaultValue: 'all', description: 'Pattern to limit builds by matching ARCH')
    string(name: 'FLAVOR_FILTER', defaultValue: 'all', description: 'Pattern to limit builds by matching FLAVOR')
    string(name: 'SLACK_CHANNEL', defaultValue: '#ide-builds', description: 'Slack channel to publish build message.')
  }

  environment {
    RSTUDIO_VERSION = ""
    RSTUDIO_VERSION_MAJOR = ""
    RSTUDIO_VERSION_MINOR = ""
    RSTUDIO_VERSION_PATCH = ""
    RSTUDIO_VERSION_SUFFIX = ""
    RSTUDIO_VERSION_FLOWER = ""
    RSTUDIO_VERSION_FILENAME = ""
    ENV = ""
    IS_PRO = false
    AWS_ACCOUNT_ID = '749683154838'
  }

  stages {

    stage('Initialize') {
      agent { label 'linux' }

      stages {
        stage('Load Utils') {
          steps {
            script {
              utils = load "${env.WORKSPACE}/jenkins/utils.groovy"
            }
          }
        }

        stage("Checkout") {
          when { expression { params.COMMIT_HASH != '' } }

          steps {
            echo "Commit_hash value: ${params.COMMIT_HASH}"
            checkout([$class: 'GitSCM',
                      branches: [[name: "${params.COMMIT_HASH}"]],
                      extensions: [],
                      userRemoteConfigs: [[credentialsId: 'posit-jenkins-rstudio', url: GIT_URL ]]])
          }
        }

        stage('Versioning') {
          steps {
            script {
              (RSTUDIO_VERSION,
                RSTUDIO_VERSION_MAJOR,
                RSTUDIO_VERSION_MINOR,
                RSTUDIO_VERSION_PATCH,
                RSTUDIO_VERSION_SUFFIX) = utils.getVersion(!params.DAILY)
              RSTUDIO_VERSION_FLOWER = utils.getFlower()
              IS_PRO = RSTUDIO_VERSION_SUFFIX.contains('pro')
              RSTUDIO_VERSION_FILENAME = utils.getVersionFilename(RSTUDIO_VERSION) // Define here for use later in utils.rebuildCheck()
              
              // Set up environment for builds.
              ENV = utils.getBuildEnv(!params.DAILY)
            }

            buildName "${RSTUDIO_VERSION}"
            archiveArtifacts artifacts: 'version/RELEASE', followSymlinks: false
          }
        }
      }
    }

    stage('Build Matrix') {
      matrix {
        when { expression { return utils.shouldBuild(params.DAILY, IS_PRO) }}

        axes {
          axis {
            name 'OS'
            values 'focal', 'jammy', 'rhel8', 'rhel9', 'opensuse15'
          }
          axis {
            name 'ARCH'
            values 'x86_64', 'arm64'
          }
          axis {
              name 'FLAVOR'
              values 'Server', 'Electron'
          }
        }

        excludes {
          exclude {
            axis {
              name 'OS'
              values 'rhel8', 'opensuse15'
            }
            axis {
              name 'ARCH'
              values 'arm64'
            }
          }

          exclude {
            axis {
              name 'OS'
              values 'rhel8', 'opensuse15', 'focal'
            }
            axis {
              name 'FLAVOR'
              values 'Electron'
            }
          }
        }

        stages {
          stage ('Build, Test and Upload'){

            // Timeout after no activity in the logs
            // Placing timeout here so only this OS/ARCH/FLAVOR build will timeout
            options {
              timeout(time: 2, unit: 'HOURS', activity: true)
            }

            agent {
              docker {
                image "jenkins/ide:${utils.getDockerTag()}"
                registryCredentialsId 'ecr:us-east-1:aws-build-role'
                registryUrl 'https://263245908434.dkr.ecr.us-east-1.amazonaws.com'
                label "${utils.getAgentLabel(ARCH)}"
              }
            }

            when {
              allOf {
                anyOf {
                  environment name: 'FLAVOR', value: 'Electron'
                  environment name: 'FLAVOR', value: 'Server'
                }
                expression { return utils.rebuildCheck() == true }
              }
            }

            environment {
              AWS_BUCKET="rstudio-ide-build"
              PRODUCT="${utils.getProductName()}"
              BUILD_LOCATION = "build-${FLAVOR}-${ext_map[env.OS].toUpperCase()}" // Don't use BUILD_DIR because it impacts the make-package script
            }

            stages {
              stage('Checkout') {
                when { expression { params.COMMIT_HASH != '' } }
                steps {
                  // We need to checkout the correct commit again here because the matrix builds run on different agents
                  checkout([$class: 'GitSCM',
                            branches: [[name: "${params.COMMIT_HASH}"]],
                            extensions: [],
                            userRemoteConfigs: [[credentialsId: 'posit-jenkins-rstudio', url: GIT_URL ]]])
                }
              }

              stage('Build and Sign') {
                environment {
                  CODESIGN_KEY = credentials('gpg-codesign-private-key-2027')
                  CODESIGN_PASS = credentials('gpg-codesign-passphrase')
                  PKG_EXTENSION = "${ext_map[env.OS]}"
                }

                steps {
                  dir('package/linux') {
                    withAWS(role: 'build', roleAccount: AWS_ACCOUNT_ID) {
                      sh "PACKAGE_OS='${package_os[env.OS]}' ${ENV} ./make-${FLAVOR.toLowerCase()}-package ${ext_map[env.OS].toUpperCase()} clean"
                    }
                    sh '../../docker/jenkins/sign-release.sh ${BUILD_LOCATION}/rstudio-*.${PKG_EXTENSION} ${CODESIGN_KEY} ${CODESIGN_PASS}'
                  }
                }
              }

              stage('Test') {
                stages {
                  stage ('Electron-Only Tests') {
                    when {
                      allOf {
                        expression { return FLAVOR == "Electron" }
                      }
                    }

                    steps {
                      dir( "src/node/desktop" ) {
                        sh './scripts/docker-run-unit-tests.sh'
                      }
                    }
                  }

                  stage ('GWT and C++ Tests') {
                    when {
                      allOf {
                        // Disable opensuse15 while investigating R test failure
                        expression { return OS != "opensuse15" }
                      }
                    }
                    stages {
                      stage("GWT Tests") {
                        steps {
                          dir ( 'package/linux/' ) {
                            sh "cd ${BUILD_LOCATION}/src/gwt && ./gwt-unit-tests.sh"
                          }
                        }
                      }
                      stage("core Tests") {
                        steps {
                          dir ( 'package/linux/' ) {
                            sh "cd ${BUILD_LOCATION}/src/cpp && ./rstudio-tests --scope core"
                          }
                        }
                      }
                      stage("rsession Tests") {
                        when {
                          allOf {
                            expression { return !(OS == "focal" &&  FLAVOR == "Electron" && IS_PRO == false) } // See https://github.com/rstudio/rstudio-pro/issues/4681  
                          }
                        }
                        steps {
                          dir ( 'package/linux/' ) {
                            sh "cd ${BUILD_LOCATION}/src/cpp && ./rstudio-tests --scope rsession"
                          }
                        }
                      }
                      stage("rserver Tests") {
                        when {
                          allOf {
                            expression { return !(OS == "focal" &&  FLAVOR == "Electron" && IS_PRO == false) } // See https://github.com/rstudio/rstudio-pro/issues/4681  
                          }
                        }
                        steps {
                          dir ( 'package/linux/' ) {
                            sh "cd ${BUILD_LOCATION}/src/cpp && ./rstudio-tests --scope rserver"
                          }
                        }
                      }
                      stage("r Tests") {
                        when {
                          allOf {
                            expression { return OS != "focal"} // See https://github.com/rstudio/rstudio-pro/issues/4681  
                          }
                        }
                        steps {
                          dir ( 'package/linux/' ) {
                            sh "cd ${BUILD_LOCATION}/src/cpp && ./rstudio-tests --scope r"
                          }
                        }
                      }
                      stage('Run Rust Unit Tests') {
                        when { expression { return FLAVOR == 'Server' && IS_PRO } }
                        steps {
                          dir ( "package/linux/${BUILD_LOCATION}" ) {
                            // Just run the nss_pwb tests for now to keep it simple
                            // We may want to consider adding a rust workspace in the future if we add more rust targets
                            sh "ninja nss_pwb-test"
                          }
                        }
                      }
                    }
                  }
                }
              }

              stage ('Upload') {

                when { expression { return params.PUBLISH } }

                environment {
                  PACKAGE_DIR = "package/linux/${BUILD_LOCATION}"
                  PACKAGE_FILE = "${utils.renameFile(PACKAGE_DIR, 'rstudio-*.' + ext_map[env.OS])}"
                  TAR_PACKAGE_DIR= "${PACKAGE_DIR}/_CPack_Packages/Linux/${ext_map[env.OS].toUpperCase()}"
                  TAR_PACKAGE_FILE = "${utils.renameTarFile(TAR_PACKAGE_DIR)}"
                  BUILD_TYPE = "sh(script: 'cat version/BUILDTYPE', returnStdout: true).trim().toLowerCase()"
                  AWS_PATH = "${FLAVOR.toLowerCase()}/${OS}/${utils.getArchForOs(OS, ARCH)}"
                }

                stages {
                  stage("Upload Package") {
                    steps {
                      withAWS(role: 'ide-build', region: 'us-east-1') {
                        retry(5) {
                          script {
                            utils.uploadPackageToS3 "${PACKAGE_DIR}/${PACKAGE_FILE}", "${AWS_PATH}/"
                          }
                        }

                        script {
                          if (FLAVOR == "Electron") {
                            retry(5) {
                              script {
                                utils.uploadPackageToS3 "${TAR_PACKAGE_DIR}/${TAR_PACKAGE_FILE}", "${AWS_PATH}/"
                              }
                            }
                          }
                        }
                      }
                    }
                  }

                  stage("Sentry Upload") {
                    when { expression { return params.DAILY } }

                    environment {
                      SENTRY_API_KEY = credentials('ide-sentry-api-key')
                    }
                    
                    steps {
                      // Upload Sentry
                      dir("package/linux/${BUILD_LOCATION}/src/cpp") {
                        retry(5) {
                          timeout(activity: true, time: 15) {
                            script {
                              utils.sentryUpload 'elf'
                            }
                          }
                        }
                      }
                    }
                  }
                  
                  stage("Publish") {
                    environment {
                      GITHUB_LOGIN = credentials('posit-jenkins-rstudio')
                      DAILIES_PATH = "${PRODUCT}/${OS}-${utils.getArchForOs(OS, ARCH)}"
                    }

                    steps {
                      dir("${PACKAGE_DIR}") {
                        script {
                          // publish build to dailies page
                          utils.publishToDailiesSite PACKAGE_FILE, DAILIES_PATH, AWS_PATH
                          utils.optionalPublishToDailies PACKAGE_FILE, DAILIES_PATH, AWS_PATH
                        }
                      }

                      dir("${TAR_PACKAGE_DIR}") {
                        script {
                          if (FLAVOR == "Electron") {
                            utils.publishToDailiesSite TAR_PACKAGE_FILE, "${DAILIES_PATH}-xcopy", AWS_PATH
                            utils.optionalPublishToDailies TAR_PACKAGE_FILE, "${DAILIES_PATH}-xcopy", AWS_PATH
                          }
                        }
                      }
                    }
                  }

                  stage ("Update Daily Build Redirects") {
                    environment {
                      RSTUDIO_ORG_PEM = credentials('www-rstudio-org-pem') 
                    }

                    when { 
                      anyOf {
                        expression { return params.PUBLISH && params.DAILY && FLAVOR == "Electron" }
                        expression { return params.PUBLISH && params.DAILY && FLAVOR == "Server" }
                      }
                    }

                    steps {
                      script {
                        utils.updateDailyRedirects "${AWS_PATH}/${PACKAGE_FILE}"
                      }
                    }
                  }
                }
              }
            }
            post {
              always {
                deleteDir()
              }
            }
          }
        }
      }
    }

    stage('Trigger Testing Jobs') {
      agent { label 'linux' }

      // Skip for hourly builds, non-published builds, and branches that don't 
      // have corresponding automation branches
      when {
        allOf {
          expression { return params.DAILY }
          expression { return params.PUBLISH }
          expression { return env.BRANCH_NAME != "release-ghost-orchid" }
          expression { return env.BRANCH_NAME != "v1.4-juliet-rose" }
        }
      }

      stages {

        stage('Trigger Automation Testing') {
          steps {
            build wait: false,
            job: "IDE/qa-${IS_PRO ? '' : 'opensource-'}automation",
            parameters: [
              string(name: 'RSTUDIO_VERSION_MAJOR', value: "${RSTUDIO_VERSION_MAJOR}"),
              string(name: 'RSTUDIO_VERSION_MINOR', value: "${RSTUDIO_VERSION_MINOR}"),
              string(name: 'RSTUDIO_VERSION_PATCH', value: "${RSTUDIO_VERSION_PATCH}"),
              string(name: 'RSTUDIO_VERSION_SUFFIX', value: "${RSTUDIO_VERSION_SUFFIX}"),
              string(name: 'SLACK_CHANNEL', value: "${params.SLACK_CHANNEL}"),
              string(name: 'BRANCH_NAME', value: "${env.BRANCH_NAME}")
            ]
          }
        }

      }
    }
  }

  post {
    always {
      node('linux') {
        deleteDir()
        sendNotifications slack_channel: SLACK_CHANNEL
      }
    }
  }
}
